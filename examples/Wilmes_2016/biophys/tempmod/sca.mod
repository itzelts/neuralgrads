
NEURON {
	SUFFIX sca
	USEION ca READ eca WRITE ica
	RANGE gbar, g, ica
}

UNITS {
	(S) = (siemens)
	(mV) = (millivolt)
	(mA) = (milliamp)
}

PARAMETER {
	gbar = 0.0 (S/cm2)
	vshift = 0 (mV)
	actF = 1
	inactF = 3
	q10 = 2.3
	temp = 23 (degC)
}

ASSIGNED {
	v	(mV)
	eca	(mV)
	ica	(mA/cm2)
	g	(S/cm2)
	celsius (degC)
	mInf
	mTau	(ms)
	hInf
	hTau	(ms)
}

STATE {
	m
	h
}

BREAKPOINT {
	SOLVE states METHOD cnexp
	g = gbar*m*m*h
	ica = g*(v-eca)
}

DERIVATIVE states {
	rates(v+vshift)
	m' = (mInf-m)/mTau
	h' = (hInf-h)/hTau
}

INITIAL {
	rates(v+vshift)
	m = mInf
	h = hInf
}

PROCEDURE rates(v(mV)) {
	LOCAL a, b, qt
	qt = q10^((celsius-temp)/10)

	a = 0.055*(-27-v)/(exp((-27-v)/3.8) - 1)/actF
	b = 0.94*exp((-75-v)/17)/actF
	mTau = 1/(a+b)/qt
	mInf = a/(a+b)

	a = 0.000457*exp((-13-v)/50)/inactF
	b = 0.0065/(exp((-v-15)/28) + 1)/inactF
	hTau = 1/(a+b)/qt
	hInf = a/(a+b)
}
